grammar org.xtext.globalTypes.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/globalTypes/MyDsl"

Model:
	protocol = GlobalProtocol | protocol = LocalProtocol
;

GlobalProtocol:
	'global' 'protocol' protocolName=ID '(' roles=Roles '){'protocol=Protocol'}'
	;
	
Roles:
	roles+=Role (',' roles+=Role)*
;

Role:
	RoleOne | RoleSet
;

RoleOne:
	'role' name=ID
;

RoleSet:
	'roleset' name=ID ':' ref=[RoleOne | ID]
;
    

Protocol:
	{Protocol} begin = (Choice | Message | Recursion | ForEach | CloseRecursion | EndProtocol)
;

EndProtocol:
	end='End'
;


Recursion:
	'rec' name=ID ':''{'
		recProtocol=Protocol
	'}'
;

CloseRecursion: //terminale
	'loop' recursionVariable=[Recursion | ID]
;

ForEach:

	'for' loopRole=RoleOne ':' '<'roleset=[RoleSet | ID] ',' refRole = [RoleOne | ID] '>' '{'
		forBody=Protocol
	'}'';'
	protocol = Protocol
;

Choice:
	'choice' 'at' role=[RoleOne | ID] '{'
		branches+=Message
	'}' ('or' '{'
		branches+=Message
	'}')*
;

Message:
	MessageBase |
	MessageQuit
;

MessageBase:
	messageType=ID ('(' (payload=Payload)? ')' | '()') 'from' sender=[RoleOne | ID] 'to' receiver=[Role | ID] '.'
	protocol = Protocol
;


MessageQuit: //terminale
	messageType = 'QUIT' '()' 'from' sender=[RoleOne | ID] 'to' receiver=[Role | ID] '.'
;

Payload:
	types+=Type (','types+=Type)*
;

LocalProtocol:
	'local' 'protocol' protocolName=ID 'at' projectedRole=ID '(' roles=Roles ')' '{'protocol=ProtocolL'}'
;


ProtocolL:
	{ProtocolL} (actions+=(MessageL | ChoiceL | ForEachL | RecursionL | CloseRecursion))* ('End')?
;

RecursionL:
	'rec' name=ID ':''{'
		recProtocol=ProtocolL
	'}'
;

MessageL:
	messageType=ID '(' payload=Payload? ')' (sendReceive=MessageType) '.'
;

MessageType:
	SenderL | ReceiverL
;

SenderL:
	'from' role=[Role | ID]
;

ReceiverL:
	'to' role=[Role | ID]
;

ChoiceL:
	'choice' 'at' roleMakingChoice=[Role | ID] '{'
		branches+=ChoiceBranchL
	'}' ('or' '{'
		branches+=ChoiceBranchL
	'}')*
;

ChoiceBranchL:
	{ChoiceBranchL} 'End' | message=MessageL
	protocol=ProtocolL
;

ForEachL:
	'foreach' eachRole=RoleOne ':' '<'roleset=[RoleSet | ID]','refrole=[RoleOne | ID]'>' '{'
		branch=ProtocolL
	'}'
;



Type:
	'int' | 'string' | 'action'  //add more
;


