grammar org.xtext.globalTypes.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/globalTypes/MyDsl"

Model:
	protocol = GlobalProtocol | protocol = LocalProtocol
;

GlobalProtocol:
	'global' 'protocol' protocolName=PROTOCOLNAME '(' roles=Roles '){'protocol=Protocol'}'
	;
	
Roles:
	roles+=Role (',' roles+=Role)*
;

Role:
	RoleOne | RoleSet
;

RoleOne:
	'role' name=ROLENAME
;

RoleSet:
	'roleset' name=ROLESETNAME ':' ref=[RoleOne | ROLENAME]
;
    

Protocol:
	{Protocol} (actions+=(Message | Choice | Recursion | ForEach | CloseRecursion))*
;

Recursion:
	'rec' name=RECNAME ':'
;

CloseRecursion:
	'loop' recursionVariable=[Recursion | RECNAME] ';'
;

ForEach: // add ref

	'foreach' eachRole=RoleOne ':' role=[RoleSet | ROLESETNAME] '{'
		branch=Protocol
	'}'
;

Choice:
	'choice' 'at' role=[RoleOne | ROLENAME] '{'
		message+=Message
		branches+=Protocol
	'}' ('or' '{'
		message+=Message
		branches+=Protocol
	'}')*
;

Message:
	messageType=MessageType '(' payload=Payload? ')' 'from' sender=[RoleOne | ROLENAME] 'to' receiver=[RoleOne | ROLENAME] ';'
;

Payload:
	{Payload} (types+=Type (','types+=Type)*) | types+=ID
;

LocalProtocol:
	'local' 'protocol' protocolName=PROTOCOLNAME 'projection' 'on' projectedRole=ROLENAME '(' roles=RolesL ')' '{'protocol=ProtocolL'}'
;

RolesL:
	roles+=RoleL (',' roles+=RoleL)*
;

RoleL:
	RoleOneL | RoleSetL
;


RoleOneL:
	'role' name=ROLENAME
;

RoleSetL:
	'roleset' name=ROLESETNAME ':' ref=[RoleOneL | ROLENAME]
;

ProtocolL:
	{ProtocolL} (actions+=(MessageL | Message | ChoiceL | ForEachL | Recursion | CloseRecursion))*
;

MessageL:
	messageType=MessageType '(' payload=Payload? ')' (target=SenderL | target=ReceiverL) ';'
;

SenderL:
	'from' sender=[RoleL | ROLENAME]
;

ReceiverL:
	'to' to=[RoleL | ROLENAME]
;

ChoiceL:
	'choice' 'at' role=[RoleL | ROLENAME] '{'
		message+=MessageL
		branches+=ProtocolL
	'}' ('or' '{'
		message+=MessageL
		branches+=ProtocolL
	'}')*
;

ForEachL:
	'foreach' eachRole=RoleOneL ':' role=[RoleSetL | ROLESETNAME] '{'
		branch=ProtocolL
	'}'
;



MessageType:
	'REQUEST' | 'INFORM' | 'AGREE' | 'REFUSE' | 'CFP' | 'QUIT'
;

Type:
	'int' | 'string' | 'action'  //add more
;


terminal ROLENAME:('a'..'z')*;
terminal ROLESETNAME:('A'..'Z') ('a'..'z')*;
terminal RECNAME:('A'..'Z')+;
terminal PROTOCOLNAME: '_'('a'..'z')*;